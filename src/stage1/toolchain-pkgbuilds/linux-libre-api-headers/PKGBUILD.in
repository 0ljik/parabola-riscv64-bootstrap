# Maintainer: David P. <megver83@parabola.nu>
# Maintainer (Arch): Anatol Pomozov <anatol.pomozov@gmail.com>

_target_arch=@LINUX_ARCH@
_target=@CHOST@
pkgname=$_target-linux-libre-api-headers
_pkgver=4.16-gnu

_archpkgver=${_pkgver%-*}
pkgver=${_pkgver//-/_}
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace ($_target)"
arch=(any)
url='https://linux-libre.fsfla.org'
license=(GPL2)
provides=("$_target-linux-api-headers=${_archpkgver}")
conflicts=("$_target-linux-api-headers")
replaces=("$_target-linux-api-headers")
source=("https://linux-libre.fsfla.org/pub/linux-libre/releases/${_pkgver}/linux-libre-${_pkgver}.tar.xz"{,.sign})
sha512sums=('c6805b3649cd71cfef272384958b33da23a00908e795592470bf711dd67886ab7d7610e7d4ae8a2230fa7483466636465ed9744444464a078de37b41c3db23e0'
            'SKIP')
validpgpkeys=('474402C8C582DAFBE389C427BCB7CF877E7D47A7') # Alexandre Oliva

build() {
  cd linux-$_archpkgver

  make ARCH=$_target_arch mrproper
  make ARCH=$_target_arch headers_check
}

package() {
  cd linux-$_archpkgver

  make INSTALL_HDR_PATH="$pkgdir/usr/$_target/" ARCH=$_target_arch V=0 headers_install

  # clean-up unnecessary files generated during install
  find "$pkgdir" \( -name .install -or -name ..install.cmd \) -delete
}
