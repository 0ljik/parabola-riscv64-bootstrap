# Maintainer (Arch): Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Andreas Grapentin <andreas@grapentin.org>

# parabola changes and rationale:
#  - adapted from aarch64-linux-gnu-gcc
#  - this is only used to bootstrap the toolchain, since you need gcc to compile
#    glibc, and glibc to compile gcc, we have this package as a 'stage1' gcc build
#    to create the initial glibc and the 'real' gcc

_target=riscv64-linux-gnu
pkgname=$_target-gcc-bootstrap
_snapshot=20171231
pkgver=7.2.0.v$_snapshot
pkgrel=1
pkgdesc='The GNU Compiler Collection - cross compiler for riscv64 target - bootstrap version. only used to build initial glibc and compiler'
arch=(x86_64)
url='https://github.com/riscv/riscv-gnu-toolchain'
license=(GPL LGPL FDL)
depends=($_target-binutils libmpc zlib)
makedepends=(git)
options=(!emptydirs !strip)
provides=(${pkgname%-bootstrap})
conflicts=(${pkgname%-bootstrap})
source=("$_target::git+https://github.com/riscv/riscv-gnu-toolchain#tag=v$_snapshot")
sha256sums=('SKIP')

prepare() {
  cd "$srcdir/$_target"

  # fix submodule repo paths
  sed -i 's#\.\.#git://github.com/riscv#' .gitmodules
  # update submodule
  git submodule update --init riscv-gcc

  cd "$srcdir/$_target/riscv-gcc"

  echo $pkgver > gcc/BASE-VER

  ./contrib/download_prerequisites

  # Do not run fixincludes
  sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" "$srcdir"/$_target/riscv-gcc/{libiberty,gcc}/configure

  rm -rf $srcdir/gcc-build
  mkdir $srcdir/gcc-build
}

build() {
  cd gcc-build

  # using -pipe causes spurious test-suite failures
  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=48565
  CFLAGS=${CFLAGS/-pipe/}
  CXXFLAGS=${CXXFLAGS/-pipe/}

  $srcdir/$_target/riscv-gcc/configure \
      --prefix=/usr \
      --program-prefix=$_target- \
      --with-local-prefix=/usr/$_target \
      --with-sysroot=/usr/$_target \
      --with-build-sysroot=/usr/$_target \
      --libdir=/usr/lib --libexecdir=/usr/lib \
      --target=$_target --host=$CHOST --build=$CHOST \
		  --with-newlib \
		  --without-headers \
		  --disable-shared \
		  --disable-threads \
      --with-system-zlib \
		  --enable-tls \
		  --enable-languages=c \
		  --disable-libatomic \
		  --disable-libmudflap \
		  --disable-libssp \
		  --disable-libquadmath \
		  --disable-libgomp \
		  --disable-nls \
		  --disable-bootstrap \
      --enable-chehcking=release \
      --disable-multilib \

	make inhibit-libc=true all-gcc
	make inhibit-libc=true all-target-libgcc
}

package() {
  cd gcc-build

	make DESTDIR="$pkgdir" inhibit-libc=true install-gcc
	make DESTDIR="$pkgdir" inhibit-libc=true install-target-libgcc

  # strip target binaries
  find "$pkgdir"/usr/lib/gcc/$_target/ -type f -and \( -name \*.a -or -name \*.o \) -exec $_target-objcopy -R .comment -R .note -R .debug_info -R .debug_aranges -R .debug_pubnames -R .debug_pubtypes -R .debug_abbrev -R .debug_line -R .debug_str -R .debug_ranges -R .debug_loc '{}' \;

  # strip host binaries
  find "$pkgdir"/usr/bin/ "$pkgdir"/usr/lib/gcc/$_target/ -type f -and \( -executable \) -exec strip '{}' \;

  # Remove files that conflict with host gcc package
  rm -r "$pkgdir"/usr/share/man/man7
  rm -r "$pkgdir"/usr/share/info
}
