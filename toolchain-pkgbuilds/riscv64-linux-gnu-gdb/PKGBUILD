# $Id$
# Maintainer (Arch): Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Andreas Grapentin <andreas@grapentin.org>

# parabola changes and rationale:
#  - adapted from aarch64-linux-gnu-gdb

_target=riscv64-linux-gnu
pkgname=$_target-gdb
_snapshot=20171231
pkgver=8.0.v$_snapshot
pkgrel=1
pkgdesc='The GNU Debugger for the riscv64 target'
arch=(x86_64)
url='https://github.com/riscv/riscv-gnu-toolchain'
license=(GPL3)
depends=(xz ncurses expat python guile2.0 gdb-common mpfr)
makedepends=(git)
options=(!emptydirs)
source=("$_target::git+https://github.com/riscv/riscv-gnu-toolchain#tag=v$_snapshot")
sha256sums=('SKIP')

prepare() {
  cd "$srcdir/$_target"

  # fix submodule repo paths
  sed -i 's#\.\.#git://github.com/riscv#' .gitmodules
  # update submodule
  git submodule update --init riscv-binutils-gdb

  cd riscv-binutils-gdb
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  cd "$srcdir/$_target/riscv-binutils-gdb"

  ./configure \
    --target=$_target \
    --prefix=/usr \
    --enable-languages=c,c++ \
    --disable-multilib \
    --with-system-readline \
    --disable-nls \
    --with-python=/usr/bin/python3 \
    --with-guile=guile-2.0 \
    --with-system-gdbinit=/etc/gdb/gdbinit

  make all-gdb
}

package() {
  cd "$srcdir/$_target/riscv-binutils-gdb"

  make DESTDIR="$pkgdir" install-gdb

  # Following files conflict with 'gdb' package
  rm -r "$pkgdir"/usr/share/info
  rm -r "$pkgdir"/usr/share/gdb
  rm -r "$pkgdir"/usr/include/gdb
  rm -r "$pkgdir"/usr/share/man/man5
}
